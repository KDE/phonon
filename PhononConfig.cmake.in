# config file for phonon library

@PACKAGE_INIT@

# known at buildtime
set(PHONON_LIB_SONAME "@PHONON_LIB_SONAME@")
set(PHONON_VERSION @PHONON_VERSION@)

set(PHONON_PULSESUPPORT @PHONON_PULSESUPPORT@)
set(PHONON_FIND_EXPERIMENTAL @PHONON_BUILD_EXPERIMENTAL@)
set(PHONON_BACKEND_DIR_SUFFIX @BACKEND_DIR_SUFFIX@)

# install locations
set(PHONON_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
set(PHONON_LIBRARY_DIR "@PACKAGE_KDE_INSTALL_LIBDIR@")
#   this technically depends on ECM. since this is only useful to backends we
#   we'll not want to drag ECM in automatically though.
set(PHONON_BACKEND_DIR "${KDE_INSTALL_QTPLUGINDIR}/${PHONON_BACKEND_DIR_SUFFIX}")
#   Do not use! Only here for trivial compatibility with old backends.
set(PHONON_BUILDSYSTEM_DIR "@PACKAGE_BUILDSYSTEM_INSTALL_DIR@")

# dependencies
include(CMakeFindDependencyMacro)
find_dependency(Qt@QT_MAJOR_VERSION@Core)
find_dependency(Qt@QT_MAJOR_VERSION@Widgets)

include("${CMAKE_CURRENT_LIST_DIR}/PhononTargets.cmake")
set(PHONON_LIBRARY Phonon::@PHONON_LIB_SONAME@)

# Find Experimental.
# Iff it was specified as component we require it.
# Else if we built with it we try to find it quietly.
# The latter part is to provide backwards compatibility as a simple finding of
# Phonon would also drag in experimental. To simulate this we'll look for it
# silenetly while not failing if it was not found. Ultimately it was the
# config consumer's responsibility to check if experimental was actually found.
# So nothing changes there. Config consumers can however now use it as a
# component to force an error when it isn't available.
if("${@PHONON_LIB_SONAME_CAMEL@_FIND_COMPONENTS}" MATCHES ".*(Experimental|experimental).*")
    find_package(@PHONON_LIB_SONAME_CAMEL@Experimental ${PHONON_VERSION} EXACT CONFIG REQUIRED
                 PATHS ${CMAKE_CURRENT_LIST_DIR}
                 NO_DEFAULT_PATH)
elseif(PHONON_FIND_EXPERIMENTAL)
    find_package(@PHONON_LIB_SONAME_CAMEL@Experimental ${PHONON_VERSION} EXACT CONFIG QUIET
                 PATHS ${CMAKE_CURRENT_LIST_DIR}
                 NO_DEFAULT_PATH)
endif()

# Convenience.
set(PHONON_LIBRARIES ${PHONON_LIBRARY} ${PHONON_EXPERIMENTAL_LIBRARY})
# The following one is only for compatibility
set(PHONON_LIBS ${PHONON_LIBRARIES})
set(PHONON_INCLUDES ${PHONON_INCLUDE_DIR} ${PHONON_INCLUDE_DIR}/KDE)
